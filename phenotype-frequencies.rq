#+ summary: Returns the number of patients in the registry that have had a phenotype code at any time
#+ tags:
#+ - Phenotype frequency
#+ defaults:
#+ 
#+ endpoint_in_url: False

PREFIX sio: <http://semanticscience.org/resource/>
select ?type (count(?type) as ?frequency) where {
  select distinct ?p ?type where {
    ?p sio:SIO_000228 ?role .  # person has role role
    ?role sio:SIO_000356 ?process . # is realized in process
    ?process sio:SIO_000229 ?output . #has output output
    ?output sio:SIO_000628 ?attribute . # output refers to attribute
    ?attribute a ?type .
    FILTER(!(?type = sio:SIO_000614)) .  # not an "attribute" type
  }
} group by ?type
